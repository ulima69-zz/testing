%include '/dados/infor/suporte/FuncoesInfor.sas';
%IniciarProcessoMysql(Cobrança Evolução, Milton);
LIBNAME CBR "/dados/infor/producao/cobranca_pj";
%LET Mes=0;

%Macro Encarteiramento(TP=1);

	DATA _NULL_;
		CALL SYMPUT('TIGR',COMPRESS('IGR.IGRREDE_'||Put(&AnoMes, Z6.)));

		IF &TP NE 2 THEN
			CALL SYMPUT('TBL','COMUM.PAI_REL_'||Put(&AnoMes, Z6.));
		ELSE CALL SYMPUT('TBL','COMUM.PAI_REL_PJ_'||Put(&AnoMes, Z6.));
	RUN;

	%Put &TBL;

	PROC SQL;
		CREATE TABLE ENCARTEIRAMENTO AS 
			SELECT A.CD_CLI AS CD_CLI, CD_PRF_DEPE AS PrefDep,
				NR_SEQL_CTRA AS Carteira, CD_TIP_CTRA AS TC
			FROM &TBL A
				INNER JOIN BASE_MCI B ON(A.CD_CLI=B.CD_CLI)
					WHERE CD_PRF_DEPE NOT IN(4777 8008 9940 9777);
		CREATE INDEX CD_CLI ON ENCARTEIRAMENTO(CD_CLI);
	QUIT;

	PROC SQL NOPRINT;
		SELECT COUNT(*) INTO :Q
			FROM (SELECT CD_CLI
			FROM ENCARTEIRAMENTO
				GROUP BY 1
					HAVING COUNT(*)>1);
	QUIT;

	%Put &Q;

	%IF &Q>0 %THEN
		%DO;

			DATA DUPLICADOS;
				SET ENCARTEIRAMENTO(Obs=0);
			RUN;

			PROC SQL;
				CREATE TABLE AUX_D AS
					SELECT CD_CLI
						FROM ENCARTEIRAMENTO
							GROUP BY 1
								HAVING COUNT(*)>1;
			RUN;

			DATA AUX_D(DROP=Seq);
				SET AUX_D;
				Seq+1;
				Grupo=CEIL(Seq/5957);
			RUN;

			PROC SQL NOPRINT;
				SELECT MAX(Grupo) INTO: Q
					FROM AUX_D;
			QUIT;

			%PUT &Q;

			%DO I=1 %TO &Q;

				PROC SQL NOPRINT;
					SELECT CD_CLI INTO: Var Separated By ', '
						FROM AUX_D
							WHERE Grupo=&I;
				QUIT;

				PROC SQL;
					CONNECT TO DB2 (AUTHDOMAIN=DB2SGCEN DATABASE=BDB2P04);
					INSERT INTO DUPLICADOS
						SELECT A.*, Carteira, TC
							FROM CONNECTION TO DB2
								(SELECT COD, COD_PREF_AGEN
									FROM DB2MCI.CLIENTE
										WHERE COD IN(&Var)
											ORDER BY COD;) A
												INNER JOIN ENCARTEIRAMENTO B ON(A.COD=B.CD_CLI AND A.COD_PREF_AGEN=B.PrefDep);
					DISCONNECT FROM DB2;
				QUIT;

				DATA ENCARTEIRAMENTO;
					SET ENCARTEIRAMENTO(WHERE=(CD_CLI NOT IN(&Var.)));
					BY CD_CLI;
				RUN;

			%END;

			PROC SORT DATA=DUPLICADOS;
				BY CD_CLI;
			RUN;

			DATA ENCARTEIRAMENTO;
				SET ENCARTEIRAMENTO DUPLICADOS;
				BY CD_CLI;
			RUN;

			PROC DELETE DATA=DUPLICADOS AUX_D;
			RUN;

		%END;

	PROC SQL NOPRINT;
		SELECT COUNT(*) INTO: Q
			FROM (SELECT DISTINCT A.CD_CLI
			FROM BASE_MCI A
				LEFT JOIN ENCARTEIRAMENTO B ON(A.CD_CLI=B.CD_CLI)
					WHERE B.CD_CLI Is Missing);
	QUIT;

	%Put &Q;

	%IF &Q>0 %THEN
		%DO;

			DATA PERDIDOS_X;
				SET ENCARTEIRAMENTO(Obs=0);
			RUN;

			PROC SQL;
				CREATE TABLE AUX_P AS
					SELECT DISTINCT A.CD_CLI
						FROM BASE_MCI A
							LEFT JOIN ENCARTEIRAMENTO B ON(A.CD_CLI=B.CD_CLI)
								WHERE B.CD_CLI Is Missing;
			RUN;

			DATA AUX_P(DROP=Seq);
				SET AUX_P;
				Seq+1;
				Grupo=CEIL(Seq/5957);
			RUN;

			PROC SQL NOPRINT;
				SELECT MAX(Grupo) INTO: Q
					FROM AUX_P;
			QUIT;

			%PUT &Q;

			%DO I=1 %TO &Q;

				PROC SQL NOPRINT;
					SELECT CD_CLI Format 9. INTO: Var Separated By ', '
						FROM AUX_P
							WHERE Grupo=&I AND CD_CLI NE .;
				QUIT;

				PROC SQL;
					CONNECT TO DB2 (AUTHDOMAIN=DB2SGCEN DATABASE=BDB2P04);
					INSERT INTO PERDIDOS_X
						SELECT *
							FROM CONNECTION TO DB2
								(SELECT COD AS CD_CLI, COD_PREF_AGEN AS PREFIXO, 
									7002 AS CARTEIRA, 700 AS TC
								FROM DB2MCI.CLIENTE
									WHERE COD IN(&Var)
										ORDER BY COD;);
					DISCONNECT FROM DB2;
				QUIT;

			%END;

			PROC SORT DATA=PERDIDOS_X;
				BY CD_CLI;
			RUN;

			DATA ENCARTEIRAMENTO;
				SET ENCARTEIRAMENTO PERDIDOS_X;
				BY CD_CLI;
			RUN;

			PROC DELETE DATA=AUX_P PERDIDOS_X;
			RUN;

		%END;

	PROC SQL NOPRINT;
		SELECT Input(PrefDep,4.) INTO: Var Separated By ' '
			FROM &TIGR
				WHERE PrefDep NE '4777' AND TipoDep In('01' '09') AND PrefSupEst NE '8481';
	QUIT;

	DATA ENCARTEIRAMENTO;
		SET ENCARTEIRAMENTO;

		IF TC NOT IN(10 16 50 56 57 60 77 303 315 321 322 328 700) AND PrefDep IN(&Var) THEN
			DO;
				Carteira=7002;
				TC=700;
			END;

		VAREJO=PrefDep IN(&Var);
	RUN;

%Mend;

DATA _NULL_;
	IF &Mes=0 THEN
		CALL SYMPUT('D1',DiaUtilAnterior(Today()));
	ELSE
		DO;
			AA=Floor(&Mes/100);
			MM=&Mes-(AA*100);
			DD=DiaUtilAnterior(IntNx('month',MDY(MM,1,AA),1));
			CALL SYMPUT('D1',DD);
		END;
RUN;

%Put &D1;

DATA _NULL_;
	CALL SYMPUT('AnoMes',Put(&D1, yymmn6.));
	CALL SYMPUT('MesAno',Put(&D1, mmyyn6.));
	CALL SYMPUT('Ini',"'"||Put(IntNx('month',&D1,0), yymmdd10.)||"'");
	CALL SYMPUT('Fim',"'"||Put(&D1, yymmdd10.)||"'");
RUN;

%Put &AnoMes &MesAno &Ini &Fim;

PROC SQL;
	CONNECT TO DB2 (AUTHDOMAIN=DB2SGCEN DATABASE=BDB2P04);
	CREATE TABLE CBR.TRF_CBR_TOTAL_&AnoMes AS
		SELECT CD_CLI, DT_EFTC_CBR_TARF Format ddmmyy10. AS DT_CBR, Sum(VLR_TRF) AS VLR_TRF
			FROM CONNECTION TO DB2
				(SELECT CD_CLI_VCLD_CT_OGM AS CD_CLI, DT_EFTC_CBR_TARF,
					VL_OPR_CBR_TARF AS VLR_TRF
				FROM DB2TFA.CBR_TARF_REC A
					INNER JOIN DB2MCI.CLIENTE B ON(A.CD_CLI_VCLD_CT_OGM=B.COD)
						WHERE CD_PRD_CBR_TARF=14 AND DT_EFTC_CBR_TARF BETWEEN &Ini AND &Fim
							AND COD_TIPO=2)
						WHERE VLR_TRF NE 0 AND CD_CLI NE 0
							GROUP BY 1, 2;
	DISCONNECT FROM DB2;
QUIT;

PROC SQL;
	CONNECT TO DB2 (AUTHDOMAIN=DB2SGCEN DATABASE=BDB2P04);
	CREATE TABLE CBR.RMSS_COBR_&AnoMes AS
		SELECT MDY(MM,DD,AA) Format ddmmyy10. AS DT_RMS, CD_CLI, NR_OPR,
			QT_TIT, VALOR
		FROM CONNECTION TO DB2
			(SELECT B.CD_CLI, A.NR_OPR_CLI_CBR AS NR_OPR, AA_PER_MVT_TIT AS AA, MM_PER_MVT_TIT AS MM,
				DD_PER_MVT_TIT AS DD, VL_TTL_TIT_GR_ITC AS VALOR, QT_TIT_GR_ITC AS QT_TIT
			FROM DB2CBR.ETTC_GR_ITC_CLI A
				INNER JOIN DB2CBR.CTR_CLI_CBR B ON(A.NR_OPR_CLI_CBR=B.NR_OPR_CLI_CBR)
				INNER JOIN DB2MCI.CLIENTE C ON(B.CD_CLI=C.COD)
					WHERE CD_TIP_GR_ITC_CBR=1 
						AND CD_FMA_ENTD_ITC IN(5, 23, 25, 54) 
						AND NR_CTRA_CBR IN(11, 17, 31, 51)
						AND C.COD_TIPO=2
						AND DD_PER_MVT_TIT<>0)
					WHERE MDY(MM,DD,AA) BETWEEN IntNx('month',&D1,0,'b') AND &D1;
	DISCONNECT FROM DB2;
QUIT;

PROC SORT DATA = CBR.TRF_CBR_TOTAL_&AnoMes;
	BY DT_CBR CD_CLI;
RUN;

PROC SQL;
	CREATE TABLE REMESSA_ATUAL AS
		SELECT DT_RMS, CD_CLI, SUM(QT_TIT) AS QT_TIT, SUM(VALOR) AS VLR_REMESSA
			FROM CBR.RMSS_COBR_&AnoMes
				GROUP BY 1, 2;
QUIT;

DATA RESUMO_ATUAL;
	MERGE REMESSA_ATUAL(RENAME=(DT_RMS=DATA)) CBR.TRF_CBR_TOTAL_&AnoMes(RENAME=(DT_CBR=DATA));
	BY DATA CD_CLI;
RUN;

PROC STDIZE OUT=RESUMO_ATUAL REPONLY MISSING=0;
	VAR _NUMERIC_;
QUIT;

DATA _NULL_;
	DD=IntNx('month',&D1,-1);
	CALL SYMPUT('MesAnt',Put(DD, yymmn6.));
RUN;

%Put &MesAnt;

PROC SQL NOPRINT;
	SELECT COUNT(DISTINCT DATA) INTO :QD
		FROM RESUMO_ATUAL;
QUIT;

%Put &QD;

PROC SQL;
	CREATE TABLE DTS_ANT AS
		SELECT DT_RMS AS DATA
			FROM CBR.RMSS_COBR_&MesAnt
				UNION
			SELECT DT_CBR
				FROM CBR.TRF_CBR_TOTAL_&MesAnt;
QUIT;

DATA DTS_ANT;
	SET DTS_ANT;
	Seq=_N_;
RUN;

PROC SQL NOPRINT;
	SELECT DATA INTO :DTS Separated By ' '
		FROM DTS_ANT
			WHERE Seq<=&QD;
RUN;

%PUT &DTS;

PROC SQL;
	CREATE TABLE REMESSA_ANT AS
		SELECT DT_RMS, CD_CLI, SUM(QT_TIT) AS QT_TIT, SUM(VALOR) AS VLR_REMESSA
			FROM CBR.RMSS_COBR_&MesAnt
				WHERE DT_RMS IN(&DTS)
					GROUP BY 1, 2;
QUIT;

DATA TARIFA_ANT;
	SET CBR.TRF_CBR_TOTAL_&MesAnt(WHERE=(DT_CBR IN(&DTS)));
RUN;

PROC SORT DATA = TARIFA_ANT;
	BY DT_CBR CD_CLI;
RUN;

DATA RESUMO_ANT;
	MERGE REMESSA_ANT(RENAME=(DT_RMS=DATA)) TARIFA_ANT(RENAME=(DT_CBR=DATA));
	BY DATA CD_CLI;
RUN;

PROC STDIZE DATA=RESUMO_ANT OUT=RESUMO_ANT REPONLY MISSING=0;
	VAR _NUMERIC_;
QUIT;

PROC SQL;
	CREATE TABLE ACM_ATUAL AS
		SELECT CD_CLI, SUM(QT_TIT) AS QT_TIT, SUM(VLR_REMESSA) AS VLR_REMESSA, SUM(VLR_TRF) AS VLR_TRF
			FROM RESUMO_ATUAL
				GROUP BY 1;
QUIT;

PROC SQL;
	CREATE TABLE ACM_ANT AS
		SELECT CD_CLI, SUM(QT_TIT) AS QT_TIT_ANT, SUM(VLR_REMESSA) AS VLR_REMESSA_ANT, SUM(VLR_TRF) AS VLR_TRF_ANT
			FROM RESUMO_ANT
				GROUP BY 1;
QUIT;

DATA RESUMO_CLIENTE;
	MERGE ACM_ANT ACM_ATUAL;
	BY CD_CLI;
RUN;

PROC STDIZE OUT=RESUMO_CLIENTE REPONLY MISSING=0;
	VAR _NUMERIC_;
QUIT;

PROC SQL;
	CREATE TABLE BASE_MCI AS
		SELECT DISTINCT CD_CLI
			FROM RESUMO_CLIENTE
				ORDER BY 1;
	CREATE INDEX CD_CLI ON BASE_MCI(CD_CLI);
QUIT;

%Encarteiramento;

DATA RESUMO_CLIENTE(drop=VAREJO);
	MERGE ENCARTEIRAMENTO(IN=A where = (varejo = 1)) RESUMO_CLIENTE;
	BY CD_CLI;

	IF A;
RUN;

PROC SQL;
	CREATE TABLE CRT AS SELECT Prefdep, Carteira, SUM(QT_TIT_ANT) AS QT_TIT_ANT, 
		SUM(VLR_REMESSA_ANT) AS VLR_REMESSA_ANT,
		SUM(VLR_TRF_ANT) AS VLR_TRF_ANT,
		SUM(QT_TIT) AS QT_TIT,
		SUM(VLR_REMESSA) AS VLR_REMESSA,
		SUM(VLR_TRF) AS VLR_TRF
	FROM RESUMO_CLIENTE GROUP BY 1,2;
QUIT;

PROC SQL;
	CREATE TABLE PAA AS SELECT A.PREFDEP, 0 AS Carteira, SUM(QT_TIT_ANT) AS QT_TIT_ANT, 
		SUM(VLR_REMESSA_ANT) AS VLR_REMESSA_ANT,
		SUM(VLR_TRF_ANT) AS VLR_TRF_ANT,
		SUM(QT_TIT) AS QT_TIT,
		SUM(VLR_REMESSA) AS VLR_REMESSA,
		SUM(VLR_TRF) AS VLR_TRF
	FROM RESUMO_CLIENTE A
		INNER JOIN IGR.IGRREDE_&ANOMES B ON (A.PREFDEP = INPUT(B.PREFDEP,4.))
			WHERE TIPODEP = '01'
				GROUP BY 1,2;
QUIT;

PROC SQL;
	CREATE TABLE AGC AS SELECT IFN(TIPODEP = '01', INPUT (PREFAGENC,4.), A.Prefdep) AS PREFDEP, 0 AS Carteira, SUM(QT_TIT_ANT) AS QT_TIT_ANT, 
		SUM(VLR_REMESSA_ANT) AS VLR_REMESSA_ANT,
		SUM(VLR_TRF_ANT) AS VLR_TRF_ANT,
		SUM(QT_TIT) AS QT_TIT,
		SUM(VLR_REMESSA) AS VLR_REMESSA,
		SUM(VLR_TRF) AS VLR_TRF
	FROM RESUMO_CLIENTE A
		INNER JOIN IGR.IGRREDE_&ANOMES B ON (A.PREFDEP = INPUT(B.PREFDEP,4.))
			WHERE TIPODEP IN ('01' '09')
				GROUP BY 1,2;
QUIT;

PROC SQL;
	CREATE TABLE GRV AS SELECT INPUT (PREFSUPREG,4.) AS PREFDEP, 0 AS Carteira, SUM(QT_TIT_ANT) AS QT_TIT_ANT, 
		SUM(VLR_REMESSA_ANT) AS VLR_REMESSA_ANT,
		SUM(VLR_TRF_ANT) AS VLR_TRF_ANT,
		SUM(QT_TIT) AS QT_TIT,
		SUM(VLR_REMESSA) AS VLR_REMESSA,
		SUM(VLR_TRF) AS VLR_TRF
	FROM AGC A
		INNER JOIN IGR.IGRREDE_&ANOMES B ON (A.PREFDEP = INPUT(B.PREFDEP,4.))
			WHERE TIPODEP IN ('01' '09')
				GROUP BY 1,2;
QUIT;

PROC SQL;
	CREATE TABLE SUP AS SELECT INPUT (PREFSUPEST,4.) AS PREFDEP, 0 AS Carteira, SUM(QT_TIT_ANT) AS QT_TIT_ANT, 
		SUM(VLR_REMESSA_ANT) AS VLR_REMESSA_ANT,
		SUM(VLR_TRF_ANT) AS VLR_TRF_ANT,
		SUM(QT_TIT) AS QT_TIT,
		SUM(VLR_REMESSA) AS VLR_REMESSA,
		SUM(VLR_TRF) AS VLR_TRF
	FROM AGC A
		INNER JOIN IGR.IGRREDE_&ANOMES B ON (A.PREFDEP = INPUT(B.PREFDEP,4.))
			WHERE TIPODEP IN ('01' '09')
				GROUP BY 1,2;
QUIT;

PROC SQL;
	CREATE TABLE DIR AS SELECT INPUT (PREFUEN,4.) AS PREFDEP, 0 AS Carteira, SUM(QT_TIT_ANT) AS QT_TIT_ANT, 
		SUM(VLR_REMESSA_ANT) AS VLR_REMESSA_ANT,
		SUM(VLR_TRF_ANT) AS VLR_TRF_ANT,
		SUM(QT_TIT) AS QT_TIT,
		SUM(VLR_REMESSA) AS VLR_REMESSA,
		SUM(VLR_TRF) AS VLR_TRF
	FROM SUP A
		INNER JOIN IGR.IGRREDE_&ANOMES B ON (A.PREFDEP = INPUT(B.PREFDEP,4.))
			GROUP BY 1,2;
QUIT;

PROC SQL;
	CREATE TABLE VIP AS SELECT 8166 AS PREFDEP, 0 AS Carteira, SUM(QT_TIT_ANT) AS QT_TIT_ANT, 
		SUM(VLR_REMESSA_ANT) AS VLR_REMESSA_ANT,
		SUM(VLR_TRF_ANT) AS VLR_TRF_ANT,
		SUM(QT_TIT) AS QT_TIT,
		SUM(VLR_REMESSA) AS VLR_REMESSA,
		SUM(VLR_TRF) AS VLR_TRF
	FROM DIR A
		INNER JOIN IGR.IGRREDE_&ANOMES B ON (A.PREFDEP = INPUT(B.PREFDEP,4.))
			GROUP BY 1,2;
QUIT;

DATA BASERPT(WHERE = (PREFDEP NE 0));
	SET CRT PAA AGC GRV SUP DIR VIP;
	BY PREFDEP CARTEIRA;
RUN;

DATA BASERPT;
	FORMAT POSICAO YYMMDD10.;
	SET BASERPT;
	QT_TIT_VAR = QT_TIT - QT_TIT_ANT;
	VLR_REMESSA_VAR = VLR_REMESSA - VLR_REMESSA_ANT;
	VLR_TRF_VAR = VLR_TRF - VLR_TRF_ANT;
	POSICAO = &D1;
RUN;

PROC SQL;
	CREATE TABLE TTL_MESANT AS
		SELECT CD_CLI, SUM(QT_TIT) AS QT_TTL_ANT, SUM(VALOR) AS RMS_TTL_ANT
			FROM CBR.RMSS_COBR_&MesAnt
				GROUP BY 1;
QUIT;

PROC SQL;
	CREATE TABLE TRF_TTL_ANT AS
		SELECT CD_CLI, SUM(VLR_TRF) AS TTL_TRF_ANT
			FROM CBR.TRF_CBR_TOTAL_&MesAnt
				GROUP BY 1;
QUIT;

DATA RESUMO_CLIENTE (DROP=TC);
	FORMAT POSICAO YYMMDD10.;
	FORMAT PREFDEP 4.;
	FORMAT CARTEIRA 5.;
	SET RESUMO_CLIENTE;
	QT_TIT_VAR = QT_TIT - QT_TIT_ANT;
	VLR_REMESSA_VAR = VLR_REMESSA - VLR_REMESSA_ANT;
	VLR_TRF_VAR = VLR_TRF - VLR_TRF_ANT;
	POSICAO = &D1;
RUN;

DATA RESUMO_CLIENTE;
	MERGE RESUMO_CLIENTE(IN=A) TTL_MESANT TRF_TTL_ANT;
	BY CD_CLI;
	QT_TTL_ANT=COALESCE(QT_TTL_ANT,0);
	RMS_TTL_ANT=COALESCE(RMS_TTL_ANT,0);
	TTL_TRF_ANT=COALESCE(TTL_TRF_ANT,0);

	IF A;
RUN;

%LET Usuario=f7176219;
%LET Keypass=aT7JQcHgN1Z8gwj55D3hNJZlH1HlXEjPswPaJ5WOlBo1hxNlaR;
%LET Rotina=cobranca-evolucao;

PROC SQL;
	CREATE TABLE TABELAS_EXPORTAR_REL (TABELA_SAS CHAR(100), ROTINA CHAR(100));
	INSERT INTO TABELAS_EXPORTAR_REL VALUES('BASERPT', 'cobranca-evolucao');
	INSERT INTO TABELAS_EXPORTAR_REL VALUES('RESUMO_CLIENTE', 'cliente');
QUIT;

%ProcessoCarregarEncerrar(TABELAS_EXPORTAR_REL);
x cd /dados/infor/producao/cobranca_pj;
x chmod 2777 *;

%EncerrarProcessoMySQL(Cobrança Evolução);

/*************************************************/;
/* TRECHO DE CÓDIGO INCLUÍDO PELO FF */;

%include "/dados/gestao/rotinas/_macros/macros_uteis.sas";
 
%processCheckOut(
    uor_resp = 341556
    ,funci_resp = &sysuserid
    /*,tipo = Indicador
    ,sistema = Indicador
    ,rotina = I0123 Indicador de Alguma Coisa*/
    ,mailto= 'F8369937' 'F2986408' 'F6794004' 'F7176219' 'F8176496' 'F9457977' 'F9631159'
);
